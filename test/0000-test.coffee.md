    should = require 'should'
    path = require 'path'
    FS = require '../lib/esl'
    start_freeswitch = require './tools/start-freeswitch'

    should(start_freeswitch).be.a.Function

FreeSwitch is not immediately accessible.

    describe 'The client', ->
      before (done) ->
        @timeout 3000
        service = start_freeswitch '../test/freeswitch-client.xml'
        setTimeout done, 2000

      describe 'when the server is started', ->

        it 'should be reachable', (done) ->
          client = FS.client (call) ->
            client.end()
            done()
          .connect 8024, '127.0.0.1'

        it 'should accept the connection', (done) ->
          client = FS.client (call) ->
            call.headers['Reply-Text'].should.eql '+OK accepted'
            client.end()
            done()
          .connect 8024, '127.0.0.1'

        it 'should reloadxml', (done) ->
          cmd = 'reloadxml'
          client = FS.client (call) ->
            # call.trace on
            # call.debug on
            outcome = call.sequence [
              -> @api cmd
              ->
                @body.should.match /\+OK \[Success\]/
                this
              ->
                @exit()
            ]
            outcome.then ->
              client.end()
              done()
            .done()
          .connect 8024, '127.0.0.1'
